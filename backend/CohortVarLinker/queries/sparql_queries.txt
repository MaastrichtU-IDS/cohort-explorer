# Query to Retrieve Studies Metadata
it takes 8.94 seconds with oxigraph
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX dc:    <http://purl.org/dc/elements/1.1/>
PREFIX ro:    <http://purl.obolibrary.org/obo/ro.owl/>
PREFIX obi:   <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX iao:   <http://purl.obolibrary.org/obo/iao.owl/>
PREFIX sio:   <http://semanticscience.org/ontology/sio.owl/>
PREFIX bfo:   <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX ncbi:  <http://purl.bioontology.org/ontology/NCBITAXON/>
PREFIX cmeo:  <https://w3id.org/CMEO/>

SELECT
  ?cohortId
  ?study_type
  (SAMPLE(?study_participants) AS ?study_participants)
  (SAMPLE(?study_objective) AS ?study_objective)
  (SAMPLE(?study_start) AS ?study_start)
  (SAMPLE(?study_end) AS ?study_end)
  (SAMPLE(?study_ongoing) AS ?study_ongoing)
  (SAMPLE(?cohortInstitution) AS ?cohortInstitution)
  (SAMPLE(?administrator) AS ?administrator)
  (SAMPLE(?administrator_email) AS ?administrator_email)
  (SAMPLE(?study_contact_person) AS ?study_contact_person)
  (SAMPLE(?study_contact_person_email) AS ?study_contact_person_email)
  (SAMPLE(?population_location) AS ?population_location)
  (SAMPLE(?language) AS ?language)
	(SAMPLE(?duration) AS ?duration) 
  (SAMPLE(?data_collection_frequency) AS ?data_collection_frequency)   # ⟵ NEW
  (SAMPLE(?interventions) AS ?interventions)
  (SAMPLE(?inclusion_labels)   AS ?inclusion_labels)   # ⟵ NEW
  (SAMPLE(?inclusion_values)   AS ?inclusion_values)   # ⟵ NEW
  (SAMPLE(?exclusion_labels)   AS ?exclusion_labels)   # ⟵ NEW
  (SAMPLE(?exclusion_values)   AS ?exclusion_values)   # ⟵ NEW
  (SAMPLE(?variable_uris) AS ?variable_uris)  # ⟵ NEW
  (SAMPLE(?primary_outcome_spec) AS ?primary_outcome_spec)   # ⟵ NEW
  (SAMPLE(?secondary_outcome_spec) AS ?secondary_outcome_spec)   # ⟵ NEW
  (SAMPLE(?morbidity) AS ?morbidity)
WHERE {
  GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {

    # --- Core study & protocol (keeps the rest anchored) ---
    ?study dc:identifier ?cohortId ;
           iao:is_about    ?descriptor ;
           ro:has_part   ?protocol .
    ?descriptor a sio:descriptor;
           rdfs:label ?study_type.
    ?protocol a obi:protocol .

    

    # --- Participants ---
       OPTIONAL {
      ?protocol ro:has_part ?participants_spec .
      ?participants_spec a cmeo:number_of_participants ;
                         cmeo:has_value ?study_participants .
    }
    
        # --- Objective ---
    OPTIONAL {
      ?protocol ro:has_part ?objective_spec .
      ?objective_spec a obi:objective_specification ;
                      cmeo:has_value ?study_objective .
    }
    
     OPTIONAL 
    { 
      		?protocol ro:has_part ?dcfrequency . 
      		?dcfrequency a cmeo:timeline_specification ; 
                  cmeo:has_value ?data_collection_frequency . 
    
    }
   
    OPTIONAL { 
      ?protocol ro:has_part ?intervention_spec.
      ?intervention_spec a cmeo:intervention_specification ; cmeo:has_value ?interventions . 
    
    }
    

    # --- Study execution / time / language / status / organization ---
 OPTIONAL {
       ?sde a obi:study_design_execution ;
                   ro:concretizes ?study;                                          
                  dc:language ?language .
      OPTIONAL {
       ?durationclass a cmeo:duration_of_observation ;
                            iao:is_about ?sde;
                            cmeo:has_value ?duration .
      }
      OPTIONAL {
         
         ?sde iao:has_time_stamp ?start_time.
         ?start_time a cmeo:start_time ; 
               cmeo:has_value ?study_start .
      }
      OPTIONAL {

          ?sde iao:has_time_stamp ?end_time.
          ?end_time   a cmeo:end_time;
             cmeo:has_value ?study_end .
      }
      OPTIONAL {
      
        ?completion_status a sio:ongoing ;
                     iao:is_about ?sde ;
                      cmeo:has_value ?study_ongoing .
      }

      
          
   OPTIONAL {
         ?sde ro:has_participant ?organization.
         
           ?organization a obi:organization ; 
                               cmeo:has_value ?cohortInstitution .
			OPTIONAL {
           ?admin a ncbi:homo_sapiens ;
                               obi:member_of ?organization;
          						cmeo:has_value ?administrator.

          OPTIONAL {
           ?admin_email a obi:email_address ;
                               iao:is_about ?admin ;
                               cmeo:has_value ?administrator_email .
          }
			
        }
        OPTIONAL {
           ?contactperson a ncbi:homo_sapiens ;
                               obi:member_of ?organization;
                               cmeo:has_value ?study_contact_person.
           ?contact_person_email a obi:email_address ;
                               iao:is_about ?contactperson ;
                               cmeo:has_value ?study_contact_person_email .
        }
        
      }
    }
  

    
  
       # --- PRIMARY outcomes (pre-aggregated) ---
    OPTIONAL {
      {
        SELECT ?study (GROUP_CONCAT(DISTINCT ?v; separator="||") AS ?primary_outcome_spec)
        WHERE {
          GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
            ?study ro:has_part ?p .
            ?p a obi:protocol ;
               ro:has_part ?outcome_spec .
            ?outcome_spec a cmeo:outcome_specification ;
                          ro:has_part ?prim_spec .
            ?prim_spec a cmeo:primary_outcome_specification ;
                       cmeo:has_value ?raw .
            BIND(STR(?raw) AS ?v)
          }
        }
        GROUP BY ?study
      }
    }

    # --- SECONDARY outcomes (pre-aggregated) ---
    OPTIONAL {
      {
        SELECT ?study (GROUP_CONCAT(DISTINCT ?v; separator="||") AS ?secondary_outcome_spec)
        WHERE {
          GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
            ?study ro:has_part ?p .
            ?p a obi:protocol ;
               ro:has_part ?outcome_spec .
            ?outcome_spec a cmeo:outcome_specification ;
                          ro:has_part ?sec_spec .
            ?sec_spec a cmeo:secondary_outcome_specification ;
                      cmeo:has_value ?raw .
            BIND(STR(?raw) AS ?v)
          }
        }
        GROUP BY ?study
      }
    }
    
      # --- VARIABLES URI (pre-aggregated) ---
    OPTIONAL {
      {
        SELECT ?study (GROUP_CONCAT(DISTINCT ?v; separator="||") AS ?variable_uris)
        WHERE {
          GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
            ?study ro:has_part ?p .
            ?p a obi:protocol;
             ro:has_part ?variable_spec.
			OPTIONAL{
              
      			?variable_spec a cmeo:study_design_variable_specification ;
                     ro:has_part ?variable_uri .
    
        
            	BIND(STR(?variable_uri) AS ?v)
              
            }
          }
        }
        GROUP BY ?study 
      }
    }
    
   {
      SELECT ?study ?population_location
             (GROUP_CONCAT(?lbl; SEPARATOR=" || ") AS ?inclusion_labels)
             (GROUP_CONCAT(?val; SEPARATOR=" || ") AS ?inclusion_values)
      		 (GROUP_CONCAT(?ex_lbl; SEPARATOR=" || ") AS ?exclusion_labels)
             (GROUP_CONCAT(?ex_val; SEPARATOR=" || ") AS ?exclusion_values)
             (GROUP_CONCAT(DISTINCT ?m; separator="; ") AS ?morbidity)
      WHERE {
        # Order the rows by a stable key (?k) so labels and values line up positionally
        SELECT ?study ?k ?lbl ?val ?ex_lbl ?ex_val ?population_location ?m
        WHERE {
          GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
            ?study ro:has_part ?protocol .
            ?protocol a obi:protocol ;
                     ro:has_part ?selection_criterion .
            ?selection_criterion a obi:eligibility_criterion ;
                                 ro:has_part ?criterion .
			
            OPTIONAL{
                    ?selection_criterion ro:is_concretized_by ?enrollment .
                    ?enrollment a cmeo:human_subject_enrollment;
                                         ro:has_output ?output_population .
                    
                    ?site iao:is_about ?output_population ; 
                          cmeo:has_value ?population_location .
              
              		 ?morbidity_spec a obi:morbidity ;
                            ro:is_characteristic_of ?output_population ;
                            cmeo:has_value ?mv .
            		BIND(STR(?mv) AS ?m)
              
            }
            # criterion type (any subclass)
            ?criterion a obi:inclusion_criterion;
                ro:has_part  ?inclusion_criterion_sub.
               

            # stable ordering key per inclusion node
            BIND(STR(?inclusion_criterion_sub) AS ?k)

            # human-friendly criterion label
            OPTIONAL { ?inclusion_criterion_sub rdfs:label ?inc_label . }
            BIND(
                STR(?inc_label)
               AS ?lbl
            )

            # derive value: literal on inclusion or its parts, or label of a value node
            OPTIONAL { ?inclusion_criterion_sub cmeo:has_value ?val_raw . }
            BIND(STR(?val_raw) AS ?val)

            # keep only rows that actually have a value
            FILTER(BOUND(?val) && ?val != "")
            
            ?exclusionlusion_criterion a obi:exclusion_criterion;
                ro:has_part  ?exclusion_criterion_sub.
            
             BIND(STR(?exclusion_criterion_sub) AS ?exc_k)
            
             OPTIONAL { ?exclusion_criterion_sub rdfs:label ?exc_label . }
            BIND(
                STR(?exc_label)
               AS ?ex_lbl
            )
            
             OPTIONAL { ?exclusion_criterion_sub cmeo:has_value ?exc_val_raw . }
            BIND(STR(?exc_val_raw) AS ?ex_val)

            # keep only rows that actually have a value
            FILTER(BOUND(?ex_val) && ?ex_val != "")
            

          }
        }
        ORDER BY ?study ?population_location ?k ?exc_k ?m
      }
      GROUP BY ?study  ?population_location
    }
  }
}
GROUP BY ?study ?cohortId ?study_type




# Query to Retrieve Variables Metadata from All studies
it takes 70 seconds with oxigraph

PREFIX stato: <http://purl.obolibrary.org/obo/stato.owl/>
PREFIX ro:   <http://purl.obolibrary.org/obo/ro.owl/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX dc:   <http://purl.org/dc/elements/1.1/>
PREFIX obi:  <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX iao:  <http://purl.obolibrary.org/obo/iao.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/>

SELECT
  ?study_graph
  ?study_name
  ?var_name
  ?var_label
  ?varType
  ?categorical_values
  ?category_concept_codes
  ?category_concept_label
  ?category_omop_id
  ?concept_labels
  ?concept_codes
  ?concept_omop_id
  ?unit_value
  ?unit_label
  ?visit
  ?omopDomain
  ?count_value
  ?formula_value
  ?minimum_value
?maximum_value
WHERE {
  # -------- base: all variables from each study graph (skip studies_metadata) --------
  GRAPH ?g {
    ?var a cmeo:data_element ;
         dc:identifier ?var_name ;
         rdfs:label ?var_label .
    OPTIONAL { ?study a cmeo:study ; rdfs:label ?_study_label . }
  }

  # Limit to your CMEO named graphs and exclude the metadata graph
  FILTER( STRSTARTS(STR(?g), "https://w3id.org/CMEO/graph/") )
  FILTER( STRAFTER(STR(?g), "https://w3id.org/CMEO/graph/") != "studies_metadata" )

  BIND(?g AS ?study_graph)
  BIND(
    COALESCE(?_study_label, REPLACE(STRAFTER(STR(?g), "/graph/"), "-", "_"))
    AS ?study_name
  )

  # ---------------- data type (one per var; SAMPLE just in case) ----------------
  OPTIONAL {
    SELECT ?g ?var (SAMPLE(?vt) AS ?varType)
    WHERE {
      GRAPH ?g {
        ?dt a cmeo:data_type ;
              iao:is_about ?var ;
            cmeo:has_value ?vt .
        # If you require: ?dt a cmeo:data_type .
      }
    } GROUP BY ?g ?var
  }
  
   # ---------------- formula (one per var; SAMPLE just in case) ----------------
  OPTIONAL {
    SELECT ?g ?var (GROUP_CONCAT(DISTINCT ?f_val; SEPARATOR=" || ") AS ?formula_value)
    WHERE {
      GRAPH ?g {
        ?formula_datum a cmeo:formula ;
              iao:is_about ?var ;
            cmeo:has_value ?f_val .
        # If you require: ?dt a cmeo:data_type .
      }
    } 
    GROUP BY ?g ?var
  }
    
     # ---------------- categorical values (value=label pairs) ----------------
    OPTIONAL {
      SELECT ?g ?var
             (GROUP_CONCAT(DISTINCT ?pair; SEPARATOR="|") AS ?categorical_value)
             

      WHERE {
        GRAPH ?g {
          ?cat_values a obi:categorical_value_specification ;
                      obi:specifies_value_of ?var ;         # <— was obi:..., should be iao:
                      cmeo:has_value ?f_val ;
                      rdfs:label ?f_label ;
        			 obi:is_specified_input_of ?catstandProc .
          BIND(CONCAT(STR(?f_val), "=", STR(?f_label)) AS ?pair)
    
          
        }
      } GROUP BY ?g ?var
    }
  
    # ---------------- categorical values standardization (value=label pairs) ----------------
    OPTIONAL {
      SELECT ?g ?var
             (GROUP_CONCAT(DISTINCT ?cat_code; SEPARATOR="|") AS ?category_concept_codes)
             (GROUP_CONCAT(DISTINCT ?cat_lbl; SEPARATOR="|") AS ?category_concept_label)
             (GROUP_CONCAT(DISTINCT ?omopStr; SEPARATOR="|") AS ?category_omop_id)
      WHERE {
        GRAPH ?g {
          
        ?cat_values a obi:categorical_value_specification ;
                     obi:specifies_value_of ?var ;
        			 obi:is_specified_input_of ?catstandProc .
        
          ?catstandProc a cmeo:data_standardization ;
             obi:has_specified_output ?code .
        
          ?code a cmeo:code;
              rdfs:label ?cat_lbl ;
                  cmeo:has_value ?cat_code ;
                  iao:denotes ?cat_omopClass .
        ?cat_omopClass a cmeo:omop_id;
                  cmeo:has_value ?cat_omop .
        BIND( STR(?cat_omop) AS ?omopStr )
          
        }
      } GROUP BY ?g ?var
    }


  # ---------------- visit(s) (concat if multiple) ----------------
  OPTIONAL {
    SELECT ?g ?var (GROUP_CONCAT(DISTINCT ?v; SEPARATOR=" || ") AS ?visit)
    WHERE {
      GRAPH ?g {
        ?visitDatum a cmeo:visit_measurement_datum ;
            		iao:is_about ?var ;
                    cmeo:has_value ?v .
        # If needed: ?visitDatum a cmeo:visit_measurement_datum .
      }
    } GROUP BY ?g ?var
  }

  # ---------------- OMOP domain(s) (concat) ----------------
  OPTIONAL {
    SELECT ?g ?var (GROUP_CONCAT(DISTINCT ?dom; SEPARATOR=" || ") AS ?omopDomain)
    WHERE {
      GRAPH ?g {
        ?var obi:is_specified_input_of ?catProc .
        ?catProc a cmeo:categorization_process;
             obi:has_specified_output ?catOut .
        ?catOut cmeo:has_value ?dom .
        # Optionally: ?catProc a cmeo:categorization_process ; ?catOut a cmeo:category .
      }
    } GROUP BY ?g ?var
  }

  # ---------------- codes + labels + OMOP IDs (concat) ----------------
 OPTIONAL {
  SELECT ?g ?var
         (GROUP_CONCAT(?lbl;      SEPARATOR=" || ") AS ?concept_labels)
         (GROUP_CONCAT(?code;     SEPARATOR=" || ") AS ?concept_codes)
         (GROUP_CONCAT(?omopStr;  SEPARATOR=" || ") AS ?concept_omop_id)
  WHERE {
    # Order first, then aggregate
    SELECT ?g ?var  ?p ?lbl ?code ?omopStr
    WHERE {
      GRAPH ?g {
        ?var obi:is_specified_input_of ?stdProc .
        ?stdProc a cmeo:data_standardization ;
                 obi:has_specified_output ?codeset .

        # take only ordered membership predicates rdf:_n
        ?codeset ?p ?codeNode .
        FILTER(STRSTARTS(STR(?p), STR(rdf:_)))

        # values
        ?codeNode cmeo:has_value ?code .

        # label (prefer en/plain; fallback to local name)
        ?codeNode rdfs:label ?lblRaw .
         BIND(STR(?lblRaw) as ?lbl)

        # OMOP mapping (may be absent → keep position with empty string)
        OPTIONAL {
          ?codeNode iao:denotes ?omopClass .
          ?omopClass a cmeo:omop_id ; cmeo:has_value ?omop_value .
          BIND(STR(?omop_value) AS ?omop_valueStr)
        }
        BIND(COALESCE(?omop_valueStr, "") AS ?omopStr)
      }
    }
    ORDER BY ?g ?var ?p 
  }
  GROUP BY ?g ?var
}

  
  

  # ---------------- unit value + label (sample) ----------------
  OPTIONAL {
    SELECT ?g ?var ?unit_value
           (SAMPLE(?uv) AS ?unit_value)
           (SAMPLE(?ul) AS ?unit_label)
    WHERE {
      GRAPH ?g {
        ?var obi:has_measurement_unit_label ?unit .
        ?unit cmeo:has_value ?uv ;
              obi:is_specified_input_of ?muStd .
        ?muStd obi:has_specified_output ?unit_code_node .
        ?unit_code_node rdfs:label ?ul .
      }
    } GROUP BY ?g ?var
  }


  # ---------------- count (sample) ----------------
  OPTIONAL {
    SELECT ?g ?var (SAMPLE(?cnt) AS ?count_value)
                 (SAMPLE(?minv) AS ?minimum_value)
             (SAMPLE(?maxv) AS ?maximum_value)
    WHERE {
      GRAPH ?g {
        ?var iao:is_denoted_by ?vnode .
        ?dataset iao:is_about ?vnode ;
                 obi:is_specified_input_of ?eda_proc .
        ?eda_proc obi:has_specified_output ?stats .
        
        
        ?stats ro:has_part ?part .
        ?part a stato:count_ ;
              cmeo:has_value ?cnt .
        
           # MIN
            ?stats ro:has_part ?minPart .
            ?minPart a stato:minimum_value; 
                   cmeo:has_value ?minv .
        	
		   # MAX
            ?stats ro:has_part ?maxPart .
            ?maxPart  a stato:maximum_value; 
                   cmeo:has_value ?maxv .
          
      }
    } GROUP BY ?g ?var
  }
}
ORDER BY ?study_name ?var_name


Query 3: Cross Mapping 

PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
          PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
          PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
          PREFIX dc:    <http://purl.org/dc/elements/1.1/>
          PREFIX ro:    <http://purl.obolibrary.org/obo/ro.owl/>
          PREFIX obi:   <http://purl.obolibrary.org/obo/obi.owl/>
          PREFIX iao:   <http://purl.obolibrary.org/obo/iao.owl/>
          PREFIX bfo:   <http://purl.obolibrary.org/obo/bfo.owl/>
          PREFIX cmeo:  <https://w3id.org/CMEO/>

          SELECT
          ?omop_id ?code_label ?code_value ?val
          (GROUP_CONCAT(DISTINCT ?varNameA; SEPARATOR=", ") AS ?source)
          (GROUP_CONCAT(DISTINCT ?varNameB; SEPARATOR=", ") AS ?target)
          (GROUP_CONCAT(DISTINCT ?visitsA ; SEPARATOR=", ") AS ?source_visit)
          (GROUP_CONCAT(DISTINCT ?visitsB ; SEPARATOR=", ") AS ?target_visit)
          WHERE {{
          {{
              # ---------- TIME-CHF (source) ----------
              SELECT
              ?omop_id ?code_label ?code_value ?val
              (GROUP_CONCAT(DISTINCT ?var_nameA; SEPARATOR=", ") AS ?varNameA)
              (GROUP_CONCAT(DISTINCT ?pairA   ; SEPARATOR=", ") AS ?visitsA)
              ("{source}" AS ?source)
              WHERE {{
              GRAPH <{graph_repo}/{source}>  {{
                  # 1) Most selective: mapping from data element -> standardized code (rdf:_1) -> OMOP
                  ?stdProcessA a cmeo:data_standardization ;
                              obi:has_specified_output ?codeSetA ;
                              obi:has_specified_input  ?dataElementA .
                  ?codeSetA rdf:_1 ?codeNodeA .
                  ?codeNodeA a cmeo:code ;
                          cmeo:has_value ?code_value ;
                          rdfs:label     ?code_label ;
                          iao:denotes    ?omopClassA .
                  ?omopClassA a cmeo:omop_id ; cmeo:has_value ?omop_id .

                  # 2) Data element identity (single valued)
                  ?dataElementA a cmeo:data_element ; dc:identifier ?var_nameA .

                  # 3) Optional category value (kept single per DE; if multi, choose one deterministically)
                  OPTIONAL {{
                  ?catProcessA a cmeo:categorization_process ;
                              obi:has_specified_input  ?dataElementA ;
                              obi:has_specified_output ?catOutA .
                  ?catOutA cmeo:has_value ?val .
                  }}

                  # 4) Visits per data element — pre-aggregate to avoid fan-out
                  OPTIONAL {{
                  {{
                      SELECT ?dataElementA (GROUP_CONCAT(DISTINCT ?visitLblA; SEPARATOR="|") AS ?visitStrA)
                      WHERE {{
                      ?visitDatumA a cmeo:visit_measurement_datum ;
                                  iao:is_about ?dataElementA ;
                                  obi:is_specified_input_of ?vsProcA .
                      ?vsProcA obi:has_specified_output ?visitCodeA .
                      ?visitCodeA rdfs:label ?visitLblA .
                      }}
                      GROUP BY ?dataElementA
                  }}
                  }}

                  # 5) Make the (var || visits) pair once, not multiplicatively
                  BIND(COALESCE(?visitStrA, "") AS ?visA)
                  BIND(CONCAT(STR(?var_nameA), "||", ?visA) AS ?pairA)
              }}
              }}
              GROUP BY ?omop_id ?code_label ?code_value ?val
          }}
          UNION
          {{
              # ---------- GISSI-HF (target) ----------
              SELECT
              ?omop_id ?code_label ?code_value ?val
              (GROUP_CONCAT(DISTINCT ?var_nameB; SEPARATOR=", ") AS ?varNameB)
              (GROUP_CONCAT(DISTINCT ?pairB   ; SEPARATOR=", ") AS ?visitsB)
              ("{target}" AS ?target)
              WHERE   {{
              GRAPH <{graph_repo}/{target}> {{
                  ?stdProcessB a cmeo:data_standardization ;
                              obi:has_specified_output ?codeSetB ;
                              obi:has_specified_input  ?dataElementB .
                  ?codeSetB rdf:_1 ?codeNodeB .
                  ?codeNodeB a cmeo:code ;
                          cmeo:has_value ?code_value ;
                          rdfs:label     ?code_label ;
                          iao:denotes    ?omopClassB .
                  ?omopClassB a cmeo:omop_id ; cmeo:has_value ?omop_id .

                  ?dataElementB a cmeo:data_element ; dc:identifier ?var_nameB .

                  OPTIONAL {{
                  ?catProcessB a cmeo:categorization_process ;
                              obi:has_specified_input  ?dataElementB ;
                              obi:has_specified_output ?catOutB .
                  ?catOutB cmeo:has_value ?val .
                  }}

                  OPTIONAL {{
                  {{
                      SELECT ?dataElementB (GROUP_CONCAT(DISTINCT ?visitLblB; SEPARATOR="|") AS ?visitStrB)
                      WHERE {{
                      ?visitDatumB a cmeo:visit_measurement_datum ;
                                  iao:is_about ?dataElementB ;
                                  obi:is_specified_input_of ?vsProcB .
                      ?vsProcB obi:has_specified_output ?visitCodeB .
                      ?visitCodeB rdfs:label ?visitLblB .
                      }}
                      GROUP BY ?dataElementB
                  }}
                  }}

                  BIND(COALESCE(?visitStrB, "") AS ?visB)
                  BIND(CONCAT(STR(?var_nameB), "||", ?visB) AS ?pairB)
              }}
              }}
              GROUP BY ?omop_id ?code_label ?code_value ?val
          }}
          }}
          GROUP BY ?omop_id ?code_label ?code_value ?val
          ORDER BY ?omop_id
